## 目录
1. [简介](#简介)
2. [ERC721相关库](#ERC721相关库)
3. [ERC721相关接口](#ERC721相关接口)
4. [ERC721主合约](#ERC721主合约)

### 简介
ERC721主合约一共引用了7个合约
```
import "./Address.sol";
import "./Context.sol";
import "./Strings.sol";
import "./IERC721.sol";
import "./IERC721Receiver.sol";
import "./IERC721Metadata.sol";
import "./ERC165.sol";
```

其中分为
* 3个库合约：`Address.sol`, `Context.sol`和`Strings.sol`
* 3个接口合约：`IERC721.sol`, `IERC721Receiver.sol`, `IERC721Metadata.sol`
* 1个EIP165合约：`ERC165.sol`

### ERC721相关库
Address库
`Address`库是`Address`变量相关函数的合集，包括判断某地址是否为合约，更安全的function call。`ERC721`用到其中的`isContract()`
```
 function isContract(address account) internal view returns (bool) {
        return account.code.length > 0;
    }
```
利用非合约地址`account.code`的长度为0的特性，从而区分某个地址是否为合约地址。

Context库
Context库封装了两个`Solidity`的`global`变量：`msg.sender`和`msg.data`。两个函数只是单纯的返回`msg.sender`和`msg.data`。所以`Context`库就是为了用函数把`msg.sender`和`msg.data`关键词包装起来，应对`solidity`未来某次升级换掉关键字的情况，没其他作用
```
abstract contract Context {
    function _msgSender() internal view virtual returns (address) {
        return msg.sender;
    }

    function _msgData() internal view virtual returns (bytes calldata) {
        return msg.data;
    }
}
```

Strings库
String库包含两个库函数：`toString()`和`toHexString()`。`toString()`把`uint256`直接转换成`string`，比如777变为”777”；而`toHexString()`把`uint256`先转换为16进制，再转换为`string`，比如170变为”0xaa”
```
function toString(uint256 value) internal pure returns (string memory) {
        if (value == 0) {
            return "0";
        }
        uint256 temp = value;
        uint256 digits;
        while (temp != 0) {
            digits++;
            temp /= 10;
        }
        bytes memory buffer = new bytes(digits);
        while (value != 0) {
            digits -= 1;
            buffer[digits] = bytes1(uint8(48 + uint256(value % 10)));
            value /= 10;
        }
        return string(buffer);
    }
```
这个函数先确定了传入的`uint256`参数是几位数，并存在`digits`变量中。然后用循环把每一位数字的`ASCII`码转换成`bytes1`，存在`buffer`中，最后把`buffer`转换成`string`返回

### ERC721相关接口
`ERC721`的主合约一共引用了4个接口合约：`IERC721.sol`, `IERC721Receiver.sol`, `ERC721Metadata.sol`,和间接引用的`ERC165`的`IERC165.sol`

IERC165接口
首先我们介绍一下`EIP165`（以太坊改进建议第165条），他的目的是创建一个标准方法来发布和检测智能合约实现的接口。

`IERC165`是`ERC165`的接口合约，只有一个函数`supportsInterface()`，输入想查询的接口的`interfaceId`，返回一个`bool`告诉你合约是否实现了该接口。
```
interface IERC165 {
    function supportsInterface(bytes4 interfaceId) external view returns (bool);
}
```

IERC721
`IERC721`是`ERC721`的接口合约，里面包括3个`event`和9个`function`：
```
interface IERC721 is IERC165 {
    event Transfer(address indexed from, address indexed to, uint256 indexed tokenId);
    event Approval(address indexed owner, address indexed approved, uint256 indexed tokenId);
    event ApprovalForAll(address indexed owner, address indexed operator, bool approved);
    
    function balanceOf(address owner) external view returns (uint256 balance);

    function ownerOf(uint256 tokenId) external view returns (address owner);

    function safeTransferFrom(address from, address to, uint256 tokenId) external;

    function transferFrom(address from, address to, uint256 tokenId) external;

    function approve(address to, uint256 tokenId) external;

    function getApproved(uint256 tokenId) external view returns (address operator);

    function setApprovalForAll(address operator, bool _approved) external;

    function isApprovedForAll(address owner, address operator) external view returns (bool);

    function safeTransferFrom(address from, address to, uint256 tokenId, bytes calldata data) external;
}
```
其中`event`包括：

`Transfer`事件：在转账时被释放，记录代币的发出地址`from`，接收地址`to`和`tokenid`。
`Approval`事件：在授权时释放，记录`approve`的发出地址`owner`，被授权地址`approved`和`tokenid`。
`ApprovalForAll`事件：在批量授权时释放，记录`approve`的发出地址`owner`，被授权地址`operator`和是否被授权`approved`。

其中`function`包括：

`balanceOf`：参数为要查询的地`address`，返回该地址的`NFT`持有量`balance`。
`ownerOf`：参数为要查询的`tokenId`，返回这个`tokenId`的主人`owner`。
`safeTransferFrom`：安全转账（如果接收方是合约地址，会要求实现ERC721的接收接口）。参数为转出地址`from`，接收地址`to`和`tokenId`。
`transferFrom`：普通转账（不检查对方是否实现ERC721的接收接口），参数为转出地址`from`，接收地址`to`和`tokenId`。
`approve`：授权，批准另一个地址使用你的NFT。参数为被授权地址`approve`和`tokenId`。
`getApproved`：查询`NFT`被批准给了哪个地址，参数为`tokenId`，返回被批准的地址`approve`。
`setApprovalForAll`：将自己持有的这类`NFT`批量授权给某个地址，参数为被授权的地址`operator`和是否授权`approved`。
`isApprovedForAll`：查询某人的`NFT`是否批量授权给了某个地址，参数为授权方`owner`和被授权地址`operator`，返回`bool`。
`safeTransferFrom`：安全转账，与3.不同的地方在于参数里面包含了`data`，可以做额外处理。


IERC721Receiver
`IERC721Receiver`接口包含了一个函数`onERC721Received()`。这个函数会在`safeTransferFrom()`中被调用，代币的接收合约必须实现这个接口才能转账成功
```
interface IERC721Receiver {
    function onERC721Received(
        address operator,
        address from,
        uint256 tokenId,
        bytes calldata data
    ) external returns (bytes4);
}
```

IERC721Metadata
`IERC721Metadata`是`ERC721`的拓展接口，实现了3个查询`metadata`的常用函数：

`name()`：返回代币名称。
`symbol()`：返回代币代号
`tokenURI()`：通过`tokenId`查询`metadata`所在`url`
```
interface IERC721Metadata is IERC721 {
    function name() external view returns (string memory);

    function symbol() external view returns (string memory);

    function tokenURI(uint256 tokenId) external view returns (string memory);
}
```

### ERC721主合约
状态变量
```
// 代币名称
    string private _name;
 
    // 代币代号
    string private _symbol;

    // tokenId到owner地址Mapping
    mapping(uint256 => address) private _owners;

    // owner地址到持币数量Mapping
    mapping(address => uint256) private _balances;

    // tokenId到授权地址Mapping
    mapping(uint256 => address) private _tokenApprovals;

    // owner地址到是否批量批准Mapping
    mapping(address => mapping(address => bool)) private _operatorApprovals;
```
* `_name`和`_symbol`是两个`string`，存储代币的名称和代号。
* `_owners`是`tokenId`到`owner`地址的`Mapping`，存储每个代币的持有人。
* `_balances`是`owner`地址到持币数量的`Mapping`，存储每个地址的持仓量。
* `_tokenApprovals`是`tokenId`到授权地址的`Mapping`，存储每个`token`的授权信息。
* `_operatorApprovals`是`owner`地址到是否批量批准的`Mapping`，存储每个`owner`的批量授权信息。注意，批量授权会把你钱包持有这个系列的所有`nft`都授权给另一个地址，别人可以随意支配


函数
`constructor`：构造函数，设定`ERC721`代币的名字和代号（`_name`和`_symbol`变量）
```
 constructor(string memory name_, string memory symbol_) {
        _name = name_;
        _symbol = symbol_;
    }
```

`supportsInterface`: 实现`IERC165`接口`supportsInterface`
```
   function supportsInterface(bytes4 interfaceId) public view virtual override(ERC165, IERC165) returns (bool) {
        return
            interfaceId == type(IERC721).interfaceId ||
            interfaceId == type(IERC721Metadata).interfaceId ||
            super.supportsInterface(interfaceId);
    }
```

`balanceOf`：实现`IERC721`的`balanceOf`，利用`_balances`变量查询`owner`地址的`balance`
```
 function balanceOf(address owner) public view virtual override returns (uint256) {
        require(owner != address(0), "ERC721: balance query for the zero address");
        return _balances[owner];
    }
```

`ownerOf`: 实现`IERC721`的`ownerOf`，利用`_owners`变量查询`tokenId`的`owner`
```
    function ownerOf(uint256 tokenId) public view virtual override returns (address) {
        address owner = _owners[tokenId];
        require(owner != address(0), "ERC721: owner query for nonexistent token");
        return owner;
    }
```

`name`：实现`IERC721Metadata`的`name`，查询代币名称
```
 function name() public view virtual override returns (string memory) {
        return _name;
    }
```

`symbol`：实现`IERC721Metadata`的`symbol`，查询代币代号
```
 function symbol() public view virtual override returns (string memory) {
        return _symbol;
    }
```

`tokenURI`：实现`IERC721Metadata`的`tokenURI`，查询代币`metadata`存放的网址。`Opensea`还有小狐狸钱包显示你`NFT`的图片，调用的就是这个函数
```
  function tokenURI(uint256 tokenId) public view virtual override returns (string memory) {
        require(_exists(tokenId), "ERC721Metadata: URI query for nonexistent token");

        string memory baseURI = _baseURI();
        return bytes(baseURI).length > 0 ? string(abi.encodePacked(baseURI, tokenId.toString())) : "";
    }
```

`_baseURI`：基于`URI`，会被`tokenURI()`调用，跟`tokenId`拼成`tokenURI`，默认为空，需要子合约重写这个函数。
```
    function _baseURI() internal view virtual returns (string memory) {
        return "";
    }

```

`approve`：实现`IERC721`的`approve`，将`tokenId`授权给`to`地址。条件：`to`不是`owner`，且`msg.sender`是`owner`或授权地址。调用`_approve`函数
```
function approve(address to, uint256 tokenId) public virtual override {
        address owner = ERC721.ownerOf(tokenId);
        require(to != owner, "ERC721: approval to current owner");

        require(
            _msgSender() == owner || isApprovedForAll(owner, _msgSender()),
            "ERC721: approve caller is not owner nor approved for all"
        );

        _approve(to, tokenId);
    }
```

`getApproved`：实现`IERC721`的`getApproved`，利用`_tokenApprovals`变量查询`tokenId`的授权地址
```
   function getApproved(uint256 tokenId) public view virtual override returns (address) {
        require(_exists(tokenId), "ERC721: approved query for nonexistent token");

        return _tokenApprovals[tokenId];
    }
```

`setApprovalForAl`l：实现`IERC721`的`setApprovalForAll`，将持有代币全部授权给`operator`地址。调用`_setApprovalForAll`函数
```
 function setApprovalForAll(address operator, bool approved) public virtual override {
        _setApprovalForAll(_msgSender(), operator, approved);
    }
```

`isApprovedForAll`：实现`IERC721`的`isApprovedForAll`，利用`_operatorApprovals`变量查询`owner`地址是否将所持`NFT`批量授权给了`operator`地址
```
  function isApprovedForAll(address owner, address operator) public view virtual override returns (bool) {
        return _operatorApprovals[owner][operator];
    }
```

`transferFrom`：实现`IERC721`的`transferFrom`，非安全转账，不建议使用。调用`_transfer`函数
```
function transferFrom(
        address from,
        address to,
        uint256 tokenId
    ) public virtual override {
        //solhint-disable-next-line max-line-length
        require(_isApprovedOrOwner(_msgSender(), tokenId), "ERC721: transfer caller is not owner nor approved");

        _transfer(from, to, tokenId);
    }
```

`safeTransferFrom`：实现`IERC721`的`safeTransferFrom`，安全转账，调用了`_safeTransfer`函数
```
  function safeTransferFrom(
        address from,
        address to,
        uint256 tokenId,
        bytes memory _data
    ) public virtual override {
        require(_isApprovedOrOwner(_msgSender(), tokenId), "ERC721: transfer caller is not owner nor approved");
        _safeTransfer(from, to, tokenId, _data);
    }

```

`_safeTransfer`：安全转账，安全地将 `tokenId`代币从`from`转移到`to`，会检查合约接收者是否了解`ERC721`协议，以防止代币被永久锁定。调用了`_transfer`函数和`_checkOnERC721Received`函数。条件：
* `from` 不能是`0`地址.
* `to` 不能是`0`地址.
* `tokenId` 代币必须存在，并且被`from`拥有.
* 如果`to`是智能合约, 他必须支持`IERC721Receiver-onERC721Received`
```
 function _safeTransfer(
        address from,
        address to,
        uint256 tokenId,
        bytes memory _data
    ) internal virtual {
        _transfer(from, to, tokenId);
        require(_checkOnERC721Received(from, to, tokenId, _data), "ERC721: transfer to non ERC721Receiver implementer");
    }
```

`_exists`：查询`tokenId`是否存在（等价于查询他的`owner`是否为非`0`地址）
```
  function _exists(uint256 tokenId) internal view virtual returns (bool) {
        return _owners[tokenId] != address(0);
    }
```

`_isApprovedOrOwner`：查询`spender`地址是否被可以使用`tokenId`(他是`owner`或被授权地址）
```
   function _isApprovedOrOwner(address spender, uint256 tokenId) internal view virtual returns (bool) {
        require(_exists(tokenId), "ERC721: operator query for nonexistent token");
        address owner = ERC721.ownerOf(tokenId);
        return (spender == owner || getApproved(tokenId) == spender || isApprovedForAll(owner, spender));
    }
```

`_safeMint`：安全`mint`函数，铸造`tokenId`并转账给`to`地址。 条件:
* `tokenId`尚不存在,
* 若`to`为智能合约，他要支持`IERC721Receiver-onERC721Received`接口
```
function _safeMint(address to, uint256 tokenId) internal virtual {
        _safeMint(to, tokenId, "");
    }
```

`_safeMint`的实现，调用了`_checkOnERC721Received`函数和`_mint`函数
```
  function _safeMint(
        address to,
        uint256 tokenId,
        bytes memory _data
    ) internal virtual {
        _mint(to, tokenId);
        require(
            _checkOnERC721Received(address(0), to, tokenId, _data),
            "ERC721: transfer to non ERC721Receiver implementer"
        );
    }
```

`_mint`：`internal`铸造函数。通过调整`_balances`和`_owners`变量来铸造`tokenId`并转账给`to`，同时释放`Tranfer`事件。条件:
* `tokenId`尚不存在。
* `to`不是`0`地址
```
function _mint(address to, uint256 tokenId) internal virtual {
        require(to != address(0), "ERC721: mint to the zero address");
        require(!_exists(tokenId), "ERC721: token already minted");

        _beforeTokenTransfer(address(0), to, tokenId);

        _balances[to] += 1;
        _owners[tokenId] = to;

        emit Transfer(address(0), to, tokenId);

        _afterTokenTransfer(address(0), to, tokenId);
    }
```

`_burn`：`internal`销毁函数，通过调整`_balances`和`_owners`变量来销毁`tokenId`，同时释放`Tranfer`事件。条件：`tokenId`存在
```
 function _burn(uint256 tokenId) internal virtual {
        address owner = ERC721.ownerOf(tokenId);

        _beforeTokenTransfer(owner, address(0), tokenId);

        // 清空授权
        _approve(address(0), tokenId);

        _balances[owner] -= 1;
        delete _owners[tokenId];

        emit Transfer(owner, address(0), tokenId);

        _afterTokenTransfer(owner, address(0), tokenId);
    }
```

`_transfer`：转账函数。通过调整`_balances`和`_owner`变量将`tokenId`从`from`转账给`to`，同时释放`Tranfer`事件。条件:
* `tokenId`被`from`拥有
* `to`不是`0`地址

```
function _transfer(
        address from,
        address to,
        uint256 tokenId
    ) internal virtual {
        require(ERC721.ownerOf(tokenId) == from, "ERC721: transfer from incorrect owner");
        require(to != address(0), "ERC721: transfer to the zero address");

        _beforeTokenTransfer(from, to, tokenId);

        // 清空授权
        _approve(address(0), tokenId);

        _balances[from] -= 1;
        _balances[to] += 1;
        _owners[tokenId] = to;

        emit Transfer(from, to, tokenId);

        _afterTokenTransfer(from, to, tokenId);
    }
```

`_approve`：授权函数。通过调整`_tokenApprovals`来，授权`to`地址操作`tokenId`，同时释放`Approval`事件
```
  function _approve(address to, uint256 tokenId) internal virtual {
        _tokenApprovals[tokenId] = to;
        emit Approval(ERC721.ownerOf(tokenId), to, tokenId);
    }
```

`_setApprovalForAll`：批量授权函数。通过调整`_operatorApprovals`变量，批量授权`to`来操作`owner`全部代币，同时释放`ApprovalForAll`事件
```
  function _setApprovalForAll(
        address owner,
        address operator,
        bool approved
    ) internal virtual {
        require(owner != operator, "ERC721: approve to caller");
        _operatorApprovals[owner][operator] = approved;
        emit ApprovalForAll(owner, operator, approved);
    }
```

`_checkOnERC721Received`：函数，用于在`to`为合约的时候调用`IERC721Receiver-onERC721Received`, 以防 `tokenId`被不小心转入黑洞
```
function _checkOnERC721Received(
        address from,
        address to,
        uint256 tokenId,
        bytes memory _data
    ) private returns (bool) {
        if (to.isContract()) {
            try IERC721Receiver(to).onERC721Received(_msgSender(), from, tokenId, _data) returns (bytes4 retval) {
                return retval == IERC721Receiver.onERC721Received.selector;
            } catch (bytes memory reason) {
                if (reason.length == 0) {
                    revert("ERC721: transfer to non ERC721Receiver implementer");
                } else {
                    assembly {
                        revert(add(32, reason), mload(reason))
                    }
                }
            }
        } else {
            return true;
        }
    }
```

`_beforeTokenTransfer`：这个函数在转账之前会被调用（包括mint和burn）。默认为空，子合约可以选择重写
```
  function _beforeTokenTransfer(
        address from,
        address to,
        uint256 tokenId
    ) internal virtual {}
```

`_afterTokenTransfer`：这个函数在转账之后会被调用（包括mint和burn）。默认为空，子合约可以选择重写
```
function _afterTokenTransfer(
        address from,
        address to,
        uint256 tokenId
    ) internal virtual {}
```